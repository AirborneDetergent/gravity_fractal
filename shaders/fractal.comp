#version 460

#define TAU 6.28318530718
#define SQRT_SAMPLES 2
#define ITERATIONS 5
#define GRAVITY 0.1
#define DOTS_PER_VERT 2
#define DOTS DOTS_PER_VERT * 4

layout(local_size_x = 32, local_size_y = 32) in;

struct Dot {
	vec2 pos;
	vec2 vel;
	float mass;
};

layout(rgba8) uniform image2D albedo;
layout(r32f) uniform image2D height;
uniform vec2 camOffset;
uniform float zoom;
uniform float dotMass;
uniform bool gridLines;
uniform vec2 resolution;

// https://nullprogram.com/blog/2018/07/31/
uint hash(uint x) {
	x += 0x9e3779b9U;
	x ^= x >> 16;
	x *= 0x7feb352dU;
	x ^= x >> 15;
	x *= 0x846ca68bU;
	x ^= x >> 16;
	return x;
}

// https://www.shadertoy.com/view/MsS3Wc
vec3 hsv2rgb(in vec3 c){
	vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),6.0)-3.0)-1.0, 0.0, 1.0);
	//rgb = rgb*rgb*(3.0-2.0*rgb); // cubic smoothing
	return c.z * mix( vec3(1.0), rgb, c.y);
}

uint getSeed(vec2 pos) {
	uvec2 signs = uvec2((sign(pos) + 1.0) * 0.5);
	pos = abs(pos);
	return (uint(pos.x) & 0x00FF) | (uint(pos.y) << 16) ^ (signs.x << 15 | signs.y << 31);
}

float seedToFloat(uint seed) {
	uint mask = (1 << 23) - 1;
	return uintBitsToFloat((floatBitsToUint(1.0) & (~mask)) | (seed & mask)) - 1.0;
}

vec4 fractal(vec2 pos) {
	float[DOTS] dirs;
	uint[4] seeds = uint[4](getSeed(pos), getSeed(pos + vec2(1.0, 0.0)), getSeed(pos + vec2(0.0, 1.0)), getSeed(pos + vec2(1.0, 1.0)));
	for(int i = 0; i < DOTS; i++) {
		int seedIndex = i % 4;
		seeds[seedIndex] = hash(seeds[seedIndex]);
		dirs[i] = seedToFloat(seeds[seedIndex]) * TAU;
	}
	vec2 p = pos - floor(pos);
	vec2 n = 1.0 - p;
	float[4] weights = float[4](n.x * n.y, p.x * n.y, n.x * p.y, p.x * p.y);
	Dot[DOTS + 1] dots;
	for(int i = 0; i < DOTS; i++) {
		dots[i] = Dot(vec2(cos(TAU * dirs[i]), sin(TAU * dirs[i])), vec2(0.0), weights[i % 4]);
	}
	dots[DOTS] = Dot(vec2(0.0), vec2(0.0), dotMass);
	float maxVel = 0.0;
	for(int i = 0; i < ITERATIONS; i++) {
		for(int d1 = 0; d1 < DOTS + 1; d1++) {
			for(int d2 = 0; d2 < DOTS + 1; d2++) {
				if(d1 != d2) {
					vec2 rel = dots[d2].pos - dots[d1].pos;
					float dist2 = dot(rel, rel);
					dots[d1].vel += dots[d2].mass * rel / dist2 * GRAVITY;
				}
			}
			dots[d1].pos += dots[d1].vel;
		}
		float vel = length(dots[DOTS].vel);
		if(vel > maxVel) {
			maxVel = vel;
		}
	}
	Dot d = dots[DOTS];
	float hue = atan(d.vel.y, d.vel.x) / TAU;
	float distTravelled = length(d.pos);
	float satVal = tanh(distTravelled / (float(ITERATIONS) * GRAVITY) * 0.2);
	return vec4(hsv2rgb(vec3(hue, satVal, satVal)), satVal * 50.0);
}

void main() {
	ivec2 texelPos = ivec2(gl_GlobalInvocationID.xy);
	if(texelPos.x >= int(resolution.x) || texelPos.y >= int(resolution.y)) {
		return;
	}
	vec2 v_pos = (vec2(texelPos) / resolution - 0.5) * 2.0;
	vec4 colorHeight = vec4(0.0);
	float aspect = resolution.x / resolution.y;
	vec2 pos = v_pos * zoom;
	pos.x *= aspect;
	pos += camOffset;
	vec2 fp = pos - floor(pos);
	if(gridLines && min(fp.x, min(fp.y, min(1.0 - fp.x, 1.0 - fp.y))) < 0.005 * zoom) {
		colorHeight = vec4(0);
	} else {
		vec2 variation = 2.0 * zoom / resolution;
		uint seed = getSeed(vec2(texelPos));
		for(float x = 0.0; x < SQRT_SAMPLES; x++) {
			for(float y = 0.0; y < SQRT_SAMPLES; y++) {
				float ox = x / SQRT_SAMPLES - 0.5 + 0.5 / SQRT_SAMPLES;
				float oy = y / SQRT_SAMPLES - 0.5 + 0.5 / SQRT_SAMPLES;
				colorHeight += fractal(pos + vec2(ox, oy) * variation);
			}
		}
		colorHeight /= float(SQRT_SAMPLES * SQRT_SAMPLES);
	}
	imageStore(albedo, texelPos, vec4(colorHeight.rgb, 1.0));
	imageStore(height, texelPos, vec4(colorHeight.a));
}